#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Long qw(GetOptions);

my $help = 0;
GetOptions("help" => \$help) or help();
help() if $help;

my $file = $ARGV[0] || help();

unless (-e $file) {
    print "Can't find file '$file'\n";
    help();
}

unless ($file =~ /\.mp4$/) {
    print "That's not an mp4 file.\n";
    help();
}

unless (-e 'titlecard.png') {
    print "You need to have a titlecard.png. You can generate it using generate_title.\n";
    help();
}

unless (-e 'endcard.png' ) {
    print "You need to have an endcard.png. See endcard_EXAMPLE.png for the correct size for a YouTube video.\n";
    help();
}

my $preroll;
if (-e 'preroll.mp4') {
    $preroll = ' preroll.mp4 -mix 30 -mixer luma ';
} else {
    $preroll = ' ';
    print "Note: There's no preroll.mp4, so we're proceeding without it.";
}

$file =~ s/\.mp4$//;
my $orig = $file . '.mp4';
my $final = $file . '_final.mp4';

# Build the command
my $command = qq{mlt-melt -profile atsc_1080p_2997 titlecard.png out=120 $preroll \"$orig\" -mix 30 -mixer luma endcard.png out=240 -mix 30 -mixer luma -consumer avformat:\"$final\" v=libx264 preset=slow crf=18 a=copy pix_fmt=yuv420p strict=-2};

# print "Running this: \n$command\n";

# And run it
qx($command);

sub help {

print qq~
Generates the final version of the video. It assumes that the following already exist:

something.mp4 (the trimmed original video)
endcard.png (an end card for the video)
preroll.mp4 (an optional preroll mp4 video file)
TITLECARD.png (a title card for the video, generated by generate_title)

Usage: $0 "something.mp4"

~;
    exit();
}
